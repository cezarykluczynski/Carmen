buildscript {
    repositories {
        mavenCentral()
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "http://repository.jboss.org/maven2" }
        maven { url "https://repo.eclipse.org/content/repositories/egit-releases/" }
        maven { url "http://repo.jenkins-ci.org/public" }
    }

    dependencies {
        ext.springBootVersion = "1.4.0.RELEASE"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.0.3"
}

Properties props = new Properties()
props.load(new FileInputStream("./src/main/resources/application.properties"))

flyway {
    url props.getProperty("postgres.url")
    driver props.getProperty("org.postgresql.Driver")
    user props.getProperty("postgres.username")
    password props.getProperty("postgres.password")
    locations "classpath:com/cezarykluczynski/carmen/db/migration"
}

repositories {
    mavenCentral()
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "http://repository.jboss.org/maven2" }
    maven { url "https://repo.eclipse.org/content/repositories/egit-releases/" }
    maven { url "http://repo.jenkins-ci.org/public" }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'checkstyle'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'

group = 'Carmen'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

springBoot {
    mainClass = "com.cezarykluczynski.carmen.Application"
}

checkstyle {
    configFile = new File("checkstyle.xml")
    toolVersion = "7.1.1"
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }

    testLogging.showStandardStreams = true

    minHeapSize = "64m"
    maxHeapSize = "256m"
    jvmArgs '-XX:MaxPermSize=256m'
}

jacoco {
    toolVersion = "0.7.7.201606060606"
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco/html"
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'com/cezarykluczynski/carmen/db/**')
        })
    }
}

dependencies {
    ext.springBootVersion = "1.4.0.RELEASE"
    compile group: 'org.springframework.boot', name: 'spring-boot', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web-services', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc', version: springBootVersion
    compile group: 'org.springframework.data', name: 'spring-data-jpa', version:'1.10.2.RELEASE'
    compile(group: 'org.springframework.data', name: 'spring-data-jdbc-core', version:'1.2.1.RELEASE') {
        exclude(module: '*')
    }

    // DB
    compile group: 'org.flywaydb', name: 'flyway-core', version:'3.2.1'
    compile group: 'org.apache.cassandra', name: 'cassandra-thrift', version:'2.2.0'
    compile group: 'org.postgresql', name: 'postgresql', version:'9.4.1207'
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version:'1.0.0.Final'
    compile group: 'com.datastax.cassandra', name: 'cassandra-driver-mapping', version:'3.1.0'

     // CXF
    compile group: 'org.apache.cxf', name: 'cxf-spring-boot-starter-jaxws', version:'3.1.7'
    compile group: 'org.apache.cxf', name: 'cxf-spring-boot-starter-jaxrs', version:'3.1.7'
    compile group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: '2.8.3'

    // GitHub
    compile group: 'com.jcabi', name: 'jcabi-github', version:'0.26'
    compile group: 'org.kohsuke', name: 'github-api', version:'1.72'
    compile group: 'org.eclipse.mylyn.github', name: 'org.eclipse.egit.github.core', version:'2.1.5'

    // Utils
    compile group: 'org.projectlombok', name: 'lombok', version:'1.16.6'
    compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
    compile group: 'joda-time', name: 'joda-time', version:'2.9.2'
    compile group: 'org.json', name: 'json', version:'20141113'
    compile group: 'com.google.code.gson', name: 'gson', version:'2.5'

    // Other
    compile group: 'javax.servlet', name: 'jstl', version:'1.2'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
    compile group: 'commons-dbcp', name: 'commons-dbcp', version:'1.4'
    compile group: 'org.apache.tomcat', name: 'tomcat-dbcp', version:'7.0.55'
    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper', version: '8.5.4'
    compile group: 'javax.json', name: 'javax.json-api', version:'1.0'
    compile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
    compile group: 'com.google.guava', name: 'guava', version:'19.0'
    compile(group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version:'3.1.0') {
        exclude(module: 'guava')
    }
    compile group: 'com.jayway.restassured', name: 'rest-assured', version:'2.8.0'
    compile group: 'org.apache.commons', name: 'commons-exec', version:'1.3'
    compile group: 'commons-codec', name: 'commons-codec', version:'1.10'
    compile group: 'org.reflections', name: 'reflections', version:'0.9.10'
    compile group: 'com.squareup', name: 'javapoet', version:'1.5.1'

    // Testing
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.4.7'
    testCompile group: 'org.spockframework', name: 'spock-core', version:'1.1-groovy-2.4-rc-1'
    testCompile group: 'org.spockframework', name: 'spock-spring', version:'1.1-groovy-2.4-rc-1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.owlike', name: 'genson', version:'1.3'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testCompile group: 'com.jayway.jsonpath', name: 'json-path-assert', version:'2.1.0'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-tomcat', version: springBootVersion
    testCompile group: 'org.mock-server', name: 'mockserver-netty', version:'3.10.2'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion) {
        exclude(module: 'logback-classic')
        exclude(module: 'spring-boot-starter-logging')
    }
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion) {
        exclude(module: 'logback-classic')
        exclude(module: 'spring-boot-starter-tomcat')
        exclude(module: 'spring-boot-starter-logging')
    }
    testCompile group: 'cglib', name: 'cglib-nodep', version:'3.2.4'
}

task cassandraMigrate(type:JavaExec) {
    main = "com.cezarykluczynski.carmen.db.CassandraMigrations"
    classpath = sourceSets.main.runtimeClasspath
}